//////////////////////
/*CALLBACK FUNCTIONS*/
//----VENTILATION
void VentComb01(string asItem, string asEntity)
{
	//To corridor01_ventilation
	SetGlobalVar("corridor01vent_crouch", 1);
	ChangeMap(	"level01_vent01_02_03_05_08.dae", //New map
			"ventlink03", //Positon on new map
			"door_ventilation_start","door_ventilation_stop", //Stop and end sound.
			0.5f , 0.5f);//Fade out and in time (seconds).
}

//----COMPUTER

////////////////////////////////////////////////////////
////////////// HACKING START ///////////////////////////
void ComputerAreaInteract01(string asEntity)
{
	if(GetLocalVar("UsedComputer") == 0)
	{	
		RemoveNotebookTask("TaskComputer");
		RemoveEntityCallback("PlayerExamine","computerarea");
		RemoveNotebookTask("TaskDoor4");
		
		SetLocalVar("UsedComputer", 1);
		SetLocalVar("varA", 1);
		
		SetPlayerActive(false);
		SetWideScreenActive(true);
		StartPlayerLookAt("lookat01",1.0f, 4);
		
		CreateTimer("1",1.2,"StartKeyStrokes",false);
		CreateTimer("2",3.2,"LookAtScreen",false);
		CreateTimer("3",4.2,"FindEMail",false);
	}
	else
	{
		AddMessageTrans("level01_08_officesave01", "UsedComputer");
	}

}

void StartKeyStrokes(string asTimer)
{
	PlaySoundEntity("keyboardsound",false);
}

void LookAtScreen(string asTimer)
{
	StartPlayerLookAt("lookat02",1.5f, 4);
}

void FindEMail(string asTimer)
{
	SetMessagesOverCallback("EndCompuerSearch");
	AddMessageTrans("level01_08_officesave01", "UsingComputer");
	AddNotebookNote("Notes", "ComputerMailDesc","Notes","ComputerMail");
	AddNotebookTask("TaskAllDoors", "Tasks","AllDoorCodes");
}

void EndCompuerSearch()
{
	SetPlayerActive(true);
	SetWideScreenActive(false);
	StopPlayerLookAt();	
}

////////////////////////////////////////////////////////
////////////// HACKING END ///////////////////////////


void ComputerAreaExamine01(string asEntity)
{
	AddNotebookTask("TaskComputer", "Tasks","OfficeComputer");
	
	RemoveEntityCallback("PlayerExamine","computerarea");
}

//----LOCKED DOOR PANEL
void Link01Interact01(string asEntity)
{
	if(GetGlobalVar("officeunlocked") == 0)
		{
		StartNumericalPanel("test",6,6,6,7,10.0f,"PanelCode");
		}
	else if(GetGlobalVar("officeunlocked") == 1)
		{
		ChangeMap(	"level01_03_corridor01.dae", //New map
				"link03", //Positon on new map
				"door_clickable_start","door_clickable_stop", //Stop and end sound.
				0.5f , 0.5f);//Fade out and in time (seconds).	
		}
}
void PanelCode(string asName, bool abCodeWasCorrect)
{
	if(abCodeWasCorrect)
	{
		SetGlobalVar("officeunlocked", 1);
		PlayGuiSound("door_code_correct", 0.8);
	}
	else
	{
		PlayGuiSound("door_code_incorrect", 0.8);
	}		
}
//////////////////////////////////////////////////////////
/*This function is run the FIRST time the map is loaded */
void OnStart()
{					
//----TEXT INIT----------------------
	//ventilation
	SetGameEntityDescriptionTrans("vent1", "Doors", "Ventilation02");
	SetGameEntityDescriptionTrans("computerarea", "level01_08_officesave01", "Computer");
	
	//remove note about ventilation
	RemoveNotebookTask("TaskVentilation");
	
//----SOUND INIT-------------------
	StopSoundEntity("keyboardsound",false);
	
//----INTERACT---------------------
	//ventilation
	AddUseCallback("venttool", "vent1", "VentComb01");
	SetGameEntityMaxInteractDist("vent1",0.95);
	//computer
	AddEntityCallback("PlayerInteract","computerarea","ComputerAreaInteract01");
	AddEntityCallback("PlayerExamine","computerarea","ComputerAreaExamine01");
	SetGameEntityMaxInteractDist("computerarea",1);
	//door
	AddEntityCallback("PlayerInteract","link01area","Link01Interact01");
	
//----AREA INIT-----------------
	SetAreaCustomIcon("link01area", "DoorLink");
	SetAreaCustomIcon("vent1", "DoorLink");
}	


/////////////////////////////////////////////////////
/*This function is run everytime the map is loaded */
void OnLoad()
{
//----VENTILATION---------------
	//Make sure player Stands when exciting ventilation
	if(GetGlobalVar("corridor01vent_crouch") == 0) SetPlayerPose("Stand");
		SetGlobalVar("corridor01vent_crouch", 2);

///////////////////////
////START AUTO SAVE////

	if(GetGlobalVar("autoload") == 7)
		{
		AutoSave();
		SetGlobalVar("autoload", 8);
		}
		
////STOP AUTO SAVE////	
//////////////////////

}

///////////////////////////////////////
/*This function is run 60 times a sec*/
void OnUpdate()
{	
	
}